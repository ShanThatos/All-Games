v00 TIC-TAC-TOE
 000
 000
 000

vp02+1g02<
>v GAME
> 10g  v
  v"0":< ^           <
  >  -#v_          v
  v"1":<       
  >  -#v_$"X",v
  v"2":<
  >  -#v_$"O",>  v
        v" Wins!"<
        >,,,,,,@
  v"3":<
  >  -#v_$"!warD"v
  @,"~"<   @,,,,,<
^
 v<<<<<<<<<<<<<<<<<
  BOARD PRINTING
  v_v#     %3+1:$<
    >52*,:1+3/3 v
 0>"|",vv!`\    <
 v  v  <_"-----"v
 v+1<,*25,,,,,  <
 >::3%1+\3/1+v
 v<<<<<<<<<<<<
 >:v>gv
   4  >:"0"`v
        v   <
   -  v _$::"0"+,^
   >| >:"1"`v
    $ v     _"X",^
    $ >:"2"`v
    $ v     _"O",^
      >"~",@
^   <

    v              <
>              52*,$v
          >"X"v
    >20g2%|   
          >"O"v
 v" Enter: "0,<
 >:#,_$v
 v  .:&<     <
  >52*>,:#,_$^
  ^"Invalid "   <
  <"Move Try " <^
  <"Again: "0$<^
 >:8`!#v_     ^
 >            ^
 |`\0: <
 >::3%1+v
 vg+1/3\<
 >"0"-!#v_    ^
 v%2g02 <
  >"1"v
 >|
  >"2"v
 v    <
 >\:3%1+v
^<p+1/3\<

WINNER CHECK
>                    ^
 v0                 <
 v+3                <
  HORIZONTAL X
 >:9-!#v_:v<
 v    $<   |  %3:+1<
          v$
  v\+1%3::<>$"1"10p  ^
  >3/1+g:"0"-#v_$$  ^
  v           <
  >"1"-#v_         ^
        >$          ^
 0
 v+3                <
  HORIZONTAL O
 >:9-!#v_:v<
 v    $<   |  %3:+1<
          v$
  v\+1%3::<>$"2"10p  ^
  >3/1+g:"0"-#v_$$  ^
  v           <
  >"2"-#v_         ^
        >$          ^
 0
 v+3                <
  VERTICAL X
 >:9-!#v_:v<
 v    $<   |  %3:+1<
          v$
  v\+1/3::<>$"1"10p  ^
  >3%1+g:"0"-#v_$$  ^
  v           <
  >"1"-#v_         ^
        >$          ^
 0
 v+3                <
  VERTICAL O
 >:9-!#v_:v<
 v    $<   |  %3:+1<
          v$
  v\+1/3::<>$"2"10p  ^
  >3%1+g:"0"-#v_$$  ^
  v           <
  >"2"-#v_         ^
        >$          ^
 0 DIAGONAL LR X
 >1+:4-#v_$"1"10p    ^
        >::v
 ^_v#!-"1"g<
 v$<
 0 DIAGONAL LR O
 >1+:4-#v_$"2"10p    ^
        >::v
 ^_v#!-"2"g<
 v$<
 0 DIAGONAL RL X
 >1+:4-#v_$"1"10p    ^
        >::4\-\v
 ^_v#!-"1"g    <
 v$<
 0 DIAGONAL RL O
 >1+:4-#v_$"2"10p    ^
        >::4\-\v
 ^_v#!-"2"g    <
 v$<
  DRAW 
 >20g8-v
^      _"3"10p       ^



